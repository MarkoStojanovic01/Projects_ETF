!Provera starta!
madr00 br (if !START then madr00);
!Citanje instrukcije!
madr01 PCout1, MOST1_2, ldMAR, incPC;
madr02 br (if hack then madr02);
madr03 eMAR, rdCPU, br (if !fcCPU then madr03);
madr04 MDRout1, ldIR0;
madr05 br (if !gropr then madr07);
madr06 stPRCOD, br madrA9;
madr07 br (if l1 then madr3D);
madr08 PCout1, MOST1_2, ldMAR, incPC;
madr09 br (if hack then madr09);
madr0A eMAR, rdCPU, br (if !fcCPU then madr0A);
madr0B MDRout1, MOST1_3, ldIR1, ldGPRAR;
madr0C br (if !gradr then madr0E);
madr0D stPRADR, br madrA9;
madr0E br (if l2_brnch then madr3D);
madr0F br (if l2_arlog then madr19);
madr10 PCout1, MOST1_2, ldMAR, incPC;
madr11 br (if hack then madr11);
madr12 eMAR, rdCPU, br (if !fcCPU then madr12);
madr13 MDRout1, ldIR2, br (if l3_jump then madr3D);
madr14 br (if l3_arlog then madr19);
madr15 PCout1, MOST1_2, ldMAR, incPC;
madr16 br (if hack then madr16);
madr17 eMAR, rdCPU, br (if !fcCPU then madr17);
madr18 MDRout1, ldIR3;
!Adresiranja!
madr19 bradr
!Regdir!
madr1A br (if store then madr3D);
madr1B br (if LDW then madr1D);
madr1C GPRout1, ldBB, br madr3D;
madr1D GPRout1, MOST1_2, ldBW, br madr3D;
!Regind! 
madr1E GPRout1, MOST1_2, ldMAR, br (if store then madr3D);
madr1F br madr31;
!Memdir!
madr20 IRDAout3, MOST3_2, ldMAR, br (if store then madr3D);
madr21 br madr31;
!Memind!
madr22 IRDAout3, MOST3_2, ldMAR;
madr23 br (if hack then madr23);
madr24 eMAR, rdCPU, br (if !fcCPU then madr24);
madr25 MDRout1, ldDWH, incMAR;
madr26 br (if hack then madr26);
madr27 eMAR, rdCPU, br (if !fcCPU then madr27);
madr28 MDRout1, ldDWL;
madr29 DWout2, ldMAR, br (if store then madr3D);
madr2A br madr31;
!Regindpom!
madr2B GPRout1, IRDAout3, ADDout2, ldMAR, br (if store then madr3D);
madr2C br madr31;
!Bxpom!
madr2D GPRout1,IRDAout3,ADDout2,ldCW,incGPRAR;
madr2E GPRout1, CWout3, ADDout2, ldMAR, br (if store then madr3D);
madr2F br madr31;
!Pcrel!
madr30 PCout1, IRDAout3, ADDout2, ldMAR, br (if store then madr3D);
!Citanje operanda!
madr31 br (if hack then madr31);
madr32 eMAR, rdCPU, br (if !fcCPU then madr32);
madr33 br (if LDW then madr35);
madr34 MDRout1, ldBB, br madr3D;
madr35 MDRout1, ldDWH, incMAR;
madr36 br (if hack then madr36);
madr37 eMAR, rdCPU, br (if !fcCPU then madr37);
madr38 MDRout1, ldDWL;
madr39 DWout2, ldBW, br madr3D;
!Imm!
madr3A br (if LDW then madr3C);
madr3B IRJAout2, MOST2_1, ldBB, br madr3D;
madr3C IRDAout3, MOST3_2, ldBW, br madr3D;
!Izvrsavanje instrukcije!
madr3D bropr;
!Nop!
madr3E br madrA9;
!Halt!
madr3F clSTART, br madr00;
!Intd!
madr40 clPSWI, br madrA9;
!Inte!
madr41 stPSWI, br madrA9;
!Trpd!
madr42 clPSWT, br madrA9;
!Trpe!
madr43 stPSWT, br madrA9;
!Ldb! 
madr44 BBout2, MOST2_1, ldAB;
madr45 ldN, ldZ, ldC, ldV, br madrA9;
!Ldw!
madr46 BWout2,ldAW, br madrA9;
!Stb!
madr47 br (if regdir then madr4C);
madr48 ABout3, mxMDR, ldMDR;
madr49 br (if hack then madr49);
madr4A eMAR, eMDR, wrCPU, br (if !fcCPU then madr4A);
madr4B br madrA9;
madr4C ABout3, wrGPR, br madrA9;
!Stw!
madr4D br (if regdir then madr55);
madr4E AWHout3, mxMDR, ldMDR;
madr4F br (if hack then madr4F);
madr50 eMAR, eMDR, wrCPU, br (if !fcCPU then madr50);
madr51 AWout3, mxMDR, ldMDR, incMAR;
madr52 br (if hack then madr52);
madr53 eMAR, eMDR, wrCPU, br (if !fcCPU then madr53);
madr54 br madrA9;
madr55 AWout3, wrGPR, br madrA9;
!Popb!
madr56 SPout2, ldMAR, decSP;
madr57 br (if hack then madr57);
madr58 eMAR, rdCPU, br (if !fcCPU then madr58);
madr59 MDRout1, ldBB, ldAB;
madr5A ldN, ldZ, ldC, ldV, br madrA9;
!Popw!
madr5B SPout2, ldMAR, decSP;
madr5C br (if hack then madr5C);
madr5D eMAR, rdCPU, br (if !fcCPU then madr5D);
madr5E SPout2, ldMAR, decSP, MDRout1, ldDWL;
madr5F br (if hack then madr5F);
madr60 eMAR, rdCPU, br (if !fcCPU then madr60);
madr61 MDRout1, ldDWH;
madr62 DWout2, ldAW, ldBW, br madrA9;
!Pushb!
madr63 incSP;
madr64 SPout2, ldMAR, ABout3, mxMDR, ldMDR;
madr65 br (if hack then madr65);
madr66 eMAR, eMDR, wrCPU, br (if !fcCPU then madr66);
madr67 br madrA9;
!Pushw!
madr68 incSP;
madr69 SPout2, ldMAR, AWHout3, mxMDR, ldMDR;
madr6A br (if hack then madr6A);
madr6B eMAR, eMDR, wrCPU, br (if !fcCPU then madr6B);
madr6C incSP;
madr6D SPout2, ldMAR, AWout3, mxMDR, ldMDR;
madr6E br (if hack then madr6E);
madr6F eMAR, eMDR, wrCPU, br (if !fcCPU then madr6F);
madr70 br madrA9;
!Ldivtp!
madr71 IVTPout1, MOST1_2, ldAW, br madrA9;
!Stivtp!
madr72 AWout3, ldIVTP, br madrA9;
!Ldimr!
madr73 IMRout2, ldAW, br madrA9;
!Stimr!
madr74 AWout3, ldIMR, br madrA9;
!Ldsp!
madr75 SPout2, ldAW, br madrA9;
!Stsp!
madr76 AWout3, ldSP, br madrA9;
!Add!
madr77 BBout2, ABout3, add, ALUout1, ldAB, ldC, ldV;
madr78 ldN, ldZ, br madrA9;
!Sub!
madr79 BBout2, ABout3, sub, ALUout1, ldAB, ldC, ldV;
madr7A ldN, ldZ, br madrA9;
!Inc!
madr7B ABout3, inc, ALUout1, ldAB, ldC, ldV;
madr7C ldN, ldZ, br madrA9;
!Dec!
madr7D ABout3, dec, ALUout1, ldAB, ldC, ldV;
madr7E ldN, ldZ, br madrA9;
!And!
madr7F BBout2, ABout3, and, ALUout1, ldAB;
madr80 ldN, ldZ, ldC, ldV, br madrA9;
!Or!
madr81 BBout2, ABout3, or, ALUout1, ldAB;
madr82 ldN, ldZ, ldC, ldV, br madrA9;
!Xor!
madr83 BBout2, ABout3, xor, ALUout1, ldAB;
madr84 ldN, ldZ, ldC, ldV, br madrA9;
!Not!
madr85 ABout3, not, ALUout1, ldAB;
madr86 ldN, ldZ, ldC, ldV, br madrA9;
!Asr, Lsr, Ror i Rorc!
madr87 shr, ldC;
madr88 ldN, ldZ, ldV, br madrA9;
!Asl, Lsl, Rol i Rolc!
madr89 shl, ldC;
madr8A ldN, ldZ, ldV, br madrA9;
!Beql,..., Blsseu!
madr8B IRBRout3, br (if !brpom then madrA9);
madr8C PCout1, IRPOMout3, ADDout2, ldPC, br madrA9;
!Jmp!
madr8D IRJAout2, ldPC, br madrA9;
!Int!
madr8E stPRINS, br madrA9;
!Jsr!
madr8F incSP;
madr90 SPout2, ldMAR, PCHout3, mxMDR, ldMDR;
madr91 br (if hack then madr91);
madr92 eMAR, eMDR, wrCPU, br (if !fcCPU then madr92);
madr93 incSP;
madr94 SPout2, ldMAR, PCLout3, mxMDR, ldMDR;
madr95 br (if hack then madr95);
madr96 eMAR, eMDR, wrCPU, br (if !fcCPU then madr96);
madr97 IRJAout2, ldPC, br madrA9;
!Rti!
madr98 SPout2, ldMAR, decSP;
madr99 br (if hack then madr99);
madr9A eMAR, rdCPU, br (if !fcCPU then madr9A);
madr9B MDRout1, ldPSWL; //mozda je moguce i MDRout1, ldPSWL, SPout2, ldMAR, decSP u istom taktu;
madr9C SPout2, ldMAR, decSP;
madr9D br (if hack then madr9D);
madr9E eMAR, rdCPU, br (if !fcCPU then madr9E);
madr9F MDRout1, ldPSWH;
!Rts!
madrA0 SPout2, ldMAR, decSP;
madrA1 br (if hack then madrA1);
madrA2 eMAR, rdCPU, br (if !fcCPU then madrA2);
madrA3 MDRout1, ldDWL;
madrA4 SPout2, ldMAR, decSP;
madrA5 br (if hack then madrA5);
madrA6 eMAR, rdCPU, br (if !fcCPU then madrA6);
madrA7 MDRout1, ldDWH;
madrA8 DWout2, ldPC, br madrA9;
!Opsluzivanje prekida!
madrA9 br (if !prekid then madr00);
!Cuvanje konteksta procesora!
madrAA incSP;
madrAB SPout2, ldMAR, PCHout3, mxMDR, ldMDR;
madrAC br (if hack then madrAC);
madrAD eMAR, eMDR, wrCPU, br (if !fcCPU then madrAD);
madrAE incSP;
madrAF SPout2, ldMAR, PCLout3, mxMDR, ldMDR;
madrB0 br (if hack then madrB0);
madrB1 eMAR, eMDR, wrCPU, br (if !fcCPU then madrB1);
madrB2 incSP;
madrB3 SPout2, ldMAR, PSWHout3, mxMDR, ldMDR;
madrB4 br (if hack then madrB4);
madrB5 eMAR, eMDR, wrCPU, br (if !fcCPU then madrB5);
madrB6 incSP;
madrB7 SPout2, ldMAR, PSWLout3, mxMDR, ldMDR;
madrB8 br (if hack then madrB8);
madrB9 eMAR, eMDR, wrCPU, br (if !fcCPU then madrB9);
!Utvrdivanje broja ulaza!
madrBA br (if !PRINS then madrBC);
madrBB IRPOMout3, ldBR, clPRINS, br madrC5;
madrBC br (if !PRCOD then madrBE);
madrBD UINTout3, ldBR, clPRCOD, br madrC5;
madrBE br (if !PRADR then madrC0);
madrBF UINTout3, ldBR, clPRADR, br madrC5;
madrC0 br (if !PRINM then madrC2);
madrC1 UINTout3, ldBR, clPRINM, br madrC5;
madrC2 br (if !printr then madrC4);
madrC3 UEXTout3, ldBR, clINTR, ldL, br madrC5;
madrC4 UINTout3, ldBR;
!Utvrdivanje adrese prekidne rutine!
madrC5 IVTPout1, IVTDSPout3, ADDout2, ldMAR;
madrC6 br (if hack then madrC6);
madrC7 eMAR, rdCPU, br (if !fcCPU then madrC7);
madrC8 MDRout1, ldDWH, incMAR;
madrC9 br (if hack then madrC9);
madrCA eMAR, rdCPU, br (if !fcCPU then madrCA);
madrCB MDRout1, ldDWL;
madrCC DWout2,ldPC,clPSWI,clPSWT, br madr00;
